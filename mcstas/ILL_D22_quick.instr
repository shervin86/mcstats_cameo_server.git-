/*******************************************************************************
*         McStas instrument definition URL=http://mcstas.risoe.dk
*
* Instrument: ILL_H512_D22
*
* %Identification
* Written by: FARHI Emmanuel (farhi@ill.fr)
* Modified by NOURBAKHSH Shervin (nourbakhsh@ill.fr) in 2020
 Date: May, 2011
* Origin:ILL
* %INSTRUMENT_SITE: ILL
*
* The H512 cold guide at the ILL, with D22
*
* %Description
*
* This model describes the H512 cold guide at the ILL, with D22.
*
* The D22 Large dynamic range small-angle diffractometer
* is fully simulated. A sample can be specified (liquid),
* with monitoring of the scattering in angular (diffraction) and energy modes
* (for spectroscopy).
* 
* %Example: lambda=4.5 Detector: D22_Detector_I=1.76653e+07
*
* Flux given at sample positions from <www.ill.fr>
* ILL_H5: D22:  nu=23589.5 [rpm] lambda=4.5 [Angs] sample=H2O_liq.qSq
* Flux 1.2e8 -- 7.1e7
*
* %Parameters
* lambda: [Angs]        central wavelength band for guide illumination and D22 velocity selector setting wavelength
* dlambda: [Angs]       half width of guide wavelength band
* D22_sample: [string]  D22 liquid/powder/amorphous sample
* D_collimation: []     
* Vin_filename: [string] name of the virtual input mcpl file
* stage:                 integer indicating the simulation stage as implemented in sim_request
*
* %End
*******************************************************************************/
DEFINE INSTRUMENT ILL_H512_D22( lambda=4.5, dlambda=.45, D22_collimation=2, string D22_sample="H2O_liq.qSq", sample_size_r=0.005, sample_size_y=0.05, string Vin_filename="none", stage=-1 )

DECLARE %{

	/* HCS (H5) source 'Measured' parameters */
	/*  double sT3=413.5,sI3=10.22e12;
	    double sT2=145.8,sI2=3.44e13;
	    double sT1=40.1 ,sI1=2.78e13;
	*/
  /* guide coating parameters */
  double gR0          = 1;
  double gQc          = 0.0216;
  double gAlpha       = 4.07;
  double gW           = 1.0/300.0;

  double Al_Thickness = 0.001;
  double gGap         = 0.001;

  double  D22_nu      = 0;

  double flag         = 0;
%}

INITIALIZE %{
  if(!strcmp("none", Vin_filename) || stage<=0) Vin_filename[0]='\0';

  /* compute H512 parameters: D22 Velocity Selector */
  D22_nu   = 3956*48.3*DEG2RAD/2/PI/lambda/0.25;
  printf("%s: D22:  nu=%g [rpm] lambda=%g [Angs] sample=%s\n",
    NAME_CURRENT_COMP, D22_nu*60, lambda, D22_sample);

%}

TRACE

/* ================================ H5 common =============================== */

COMPONENT H512_Before_VS = Progress_bar()
AT(0,0,0) ABSOLUTE

COMPONENT QuickSource = Source_simple(
	xwidth = 0.04,
	yheight = 0.04,
	target_index = +5,
	focus_xw = sample_size_r,
	focus_yh = sample_size_y,
	lambda0     = lambda,
	dlambda     = dlambda,
	flux=1e13,
	gauss=0)
AT (0, 0, 0) RELATIVE H512_Before_VS



COMPONENT D22_Sample_Pos = Arm()
AT (0,0,20+0.3+0.3) RELATIVE H512_Before_VS

COMPONENT H51_D22_Sample_Div = Monitor_nD(xwidth=0.02, yheight=0.05,
  options="dx limits=[-2 2], dy limits=[-2 2]", bins=100, restore_neutron=1)
AT (0,0,0) RELATIVE D22_Sample_Pos

COMPONENT H51_D22_Sample_XY = Monitor_nD(xwidth=0.02, yheight=0.05,
  options="x y", bins=50, restore_neutron=1)
AT (0,0,0) RELATIVE D22_Sample_Pos

COMPONENT H51_D22_Sample_L = Monitor_nD(xwidth=0.02, yheight=0.05,
  options="lambda limits=[1 10]", bins=50, restore_neutron=1)
AT (0,0,0) RELATIVE D22_Sample_Pos

COMPONENT H51_D22_Sample = Isotropic_Sqw(radius=sample_size_r, yheight=sample_size_y,
  Sqw_coh=D22_sample, Sqw_inc=NULL, d_phi=RAD2DEG*atan2(1, D22_collimation))
AT (0,0,0) RELATIVE D22_Sample_Pos
EXTEND %{
  if (!SCATTERED) ABSORB;
%}

COMPONENT Slit = Slit(xmin=-0.6, xmax=0.6, ymin=-0.6, ymax=0.6)
AT(0,0,D22_collimation-0.011) RELATIVE D22_Sample_Pos
	
/*1 cm before the detector */
COMPONENT Vout = MCPL_output( filename = "sDETECTOR")
AT (0, 0, D22_collimation-0.01) RELATIVE PREVIOUS

COMPONENT D22_Detector = Monitor_nD(xwidth=0.98, yheight=1.024, options="x bins=128 y bins=256")
AT (0,0,D22_collimation) RELATIVE D22_Sample_Pos
EXTEND %{
  ABSORB; /* H512 end here */
%}

/* ========================= end of simulation ============================== */

END

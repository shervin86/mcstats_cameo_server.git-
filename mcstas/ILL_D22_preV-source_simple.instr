/*******************************************************************************
*         McStas instrument definition URL=http://mcstas.risoe.dk
*
* Instrument: ILL_H512_D22
*
* %Identification
* Written by: FARHI Emmanuel (farhi@ill.fr)
* Date: May, 2011
* Origin:ILL
* %INSTRUMENT_SITE: ILL
*
* The H512 cold guide at the ILL, with D22
*
* %Description
*
* This model describes the H512 cold guide at the ILL, with D22.
*
* The D22 Large dynamic range small-angle diffractometer
* is fully simulated. A sample can be specified (liquid),
* with monitoring of the scattering in angular (diffraction) and energy modes
* (for spectroscopy).
* 
* %Example: lambda=4.5 Detector: D22_Detector_I=1.76653e+07
*
* Flux given at sample positions from <www.ill.fr>
* ILL_H5: D22:  nu=23589.5 [rpm] lambda=4.5 [Angs] sample=H2O_liq.qSq
* Flux 1.2e8 -- 7.1e7
*
* %Parameters
* lambda: [Angs]        central wavelength band for guide illumination and D22 velocity selector setting wavelength
* dlambda: [Angs]       half width of guide wavelength band
* D22_sample: [string]  D22 liquid/powder/amorphous sample
* D_collimation: []     
*
* %End
*******************************************************************************/
DEFINE INSTRUMENT ILL_H512_D22( lambda=4.5, dlambda=.45, D22_collimation=2, string D22_sample="H2O_liq.qSq", sample_size_r=0.005, sample_size_y=0.05 )

DECLARE %{

	/* HCS (H5) source 'Measured' parameters */
	/*  double sT3=413.5,sI3=10.22e12;
	    double sT2=145.8,sI2=3.44e13;
	    double sT1=40.1 ,sI1=2.78e13;
	*/
  /* guide coating parameters */
  double gR0          = 1;
  double gQc          = 0.0216;
  double gAlpha       = 4.07;
  double gW           = 1.0/300.0;

  double Al_Thickness = 0.001;
  double gGap         = 0.001;

  double  D22_nu      = 0;

  double flag         = 0;
%}

INITIALIZE %{

  /* compute H512 parameters: D22 Velocity Selector */
  D22_nu   = 3956*48.3*DEG2RAD/2/PI/lambda/0.25;
  /*  printf("%s: D22:  nu=%g [rpm] lambda=%g [Angs] sample=%s\n",
    NAME_CURRENT_COMP, D22_nu*60, lambda, D22_sample);
  */
%}

TRACE

/* ================================ H5 common =============================== */

COMPONENT Origin = Progress_bar()
AT(0,0,0) ABSOLUTE

	/*this defines the source HCS*/
%include "ILL_source_simple.instr"

/* Ni common guide: wide entry window for all guides */
COMPONENT H5 = Monitor_nD(xwidth=0.04+0.004+0.015+0.004+0.06, yheight=0.12,
  options="x y, slit", bins=50)
AT(0,0,2.155) RELATIVE HCS

	/*COMPONENT H51_origin = COPY(H5)(options="dx limits=[-6 6] dy limits=[-6 6]", xwidth=0.04, restore_neutron=1)
AT (-(0.04+0.004+0.015+0.004+0.06)/2+0.04/2,0,0) RELATIVE H5 ROTATED (0,-1.3269,0) RELATIVE H5
	*/
	
COMPONENT H51_inpile = Guide_gravity(h1=0.12,w1=0.04, m=1.2, l=2.775,
  R0=gR0, Qc=gQc, alpha=gAlpha, W=gW)
AT (0,0,0) RELATIVE PREVIOUS

	
COMPONENT D22_PreV = Monitor_nD(xwidth=0.04, yheight=0.05,
  options="x y", bins=50, restore_neutron=1)
AT (0,0,0.01) RELATIVE PREVIOUS

/* D22 Velocity Selector: just like D11 EADS/Astrium NVS 023 in 60 cm gap */
COMPONENT D22_Vselector = V_selector(xwidth=0.04, yheight=0.05, zdepth=0.30, radius=0.12,
  alpha=48.3, length=0.25, d=0.0004, nu=D22_nu, nslit=72)
AT (0,0,3+0.01+0.3+0.01) RELATIVE PREVIOUS

COMPONENT D22_Collimation = COPY(H51_inpile)(
  l=20-D22_collimation, nelements=ceil(D22_collimation/1.5))
AT (0,0,0.3) RELATIVE D22_Vselector

COMPONENT D22_Sample_Pos = Arm()
AT (0,0,20+0.3) RELATIVE D22_Vselector

COMPONENT H51_D22_Sample_Div = Monitor_nD(xwidth=0.02, yheight=0.05,
  options="dx limits=[-2 2], dy limits=[-2 2]", bins=100, restore_neutron=1)
AT (0,0,0) RELATIVE D22_Sample_Pos

COMPONENT H51_D22_Sample_XY = Monitor_nD(xwidth=0.02, yheight=0.05,
  options="x y", bins=50, restore_neutron=1)
AT (0,0,0) RELATIVE D22_Sample_Pos

COMPONENT H51_D22_Sample_L = Monitor_nD(xwidth=0.02, yheight=0.05,
  options="lambda limits=[1 10]", bins=50, restore_neutron=1)
AT (0,0,0) RELATIVE D22_Sample_Pos

SPLIT COMPONENT H51_D22_Sample = Isotropic_Sqw(radius=sample_size_r, yheight=sample_size_y,
  Sqw_coh=D22_sample, Sqw_inc=NULL, d_phi=RAD2DEG*atan2(1, D22_collimation))
AT (0,0,0) RELATIVE D22_Sample_Pos
EXTEND %{
  if (!SCATTERED) ABSORB;
%}

COMPONENT D22_Detector = Monitor_nD(xwidth=1, yheight=1, options="x bins=128 y bins=256")
AT (0,0,D22_collimation) RELATIVE D22_Sample_Pos
EXTEND %{
  ABSORB; /* H512 end here */
%}

/* ========================= end of simulation ============================== */

END
